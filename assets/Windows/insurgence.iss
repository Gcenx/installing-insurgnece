; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Insurgence"
#define MyAppVersion "1.2.7"
#define MyAppPublisher "Insurgence"
#define MyAppURL "https://p-insurgence.com"
#define MyAppExeName "Pokemon Insurgence 1.2.7 Core\Game.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{583D0764-9C54-417D-B07A-EA4A6D630D0F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{tmp}\download.zip"; DestDir: "{app}"; Flags: external deleteafterinstall
Source: "7za.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "Fonts\pkmndp.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Clear"
Source: "Fonts\pkmndpb.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Clear Bold"
Source: "Fonts\pkmnem.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Green"
Source: "Fonts\pkmnemn.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Green Narrow"
Source: "Fonts\pkmnems.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Green Small"
Source: "Fonts\pkmnfl.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Red and Green"
Source: "Fonts\pkmnrs.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Red and Blue"
Source: "Fonts\pkmnrsi.ttf"; DestDir: "{autofonts}"; Flags: ignoreversion onlyifdoesntexist uninsneveruninstall; FontInstall: "Power Red and Blue Intl"

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Dirs]
Name: "{app}\Pokemon Insurgence 1.2.7 Core"; Permissions: users-full;

[Run]
Filename: {tmp}\7za.exe; Parameters: "x ""{tmp}\download.zip"" -o""{app}\"" * -r -aoa"; Flags: runhidden runascurrentuser;
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent;

[Code]
var DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then begin
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  end;
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    DownloadPage.Add('https://p-insurgence.com/releases/1.2.7/Pokemon%20Insurgence%201.2.7%20Core.zip', 'download.zip', '');
    DownloadPage.Show;
    try
      try
        DownloadPage.Download;
        Result := True;
      except
        SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;
